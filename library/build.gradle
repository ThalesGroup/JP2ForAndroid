plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def versionName = "1.0.3"

android {
    namespace = "com.gemalto.jp2"

    compileSdk 34
    defaultConfig {
        minSdkVersion 23
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    buildFeatures {
        buildConfig = true
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }
    ndkVersion '27.0.12077973'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.annotation:annotation:1.8.2'
    //testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

/*************************************************************/
/******************* PUBLISHING STUFF ************************/
/*************************************************************/
//create a sources jar
task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

tasks.withType(AbstractPublishToMaven) {
    dependsOn assemble
}

//publish to a local Maven repository with the sources jar and all dependencies
publishing {
    publications {
        aarRelease(MavenPublication) {
            groupId 'com.gemalto.jp2'
            artifactId 'jp2-android'
            version "${versionName}"

            from components.findByName('release')
            artifact(sourceJar)

            pom {
                name = 'JP2ForAndroid'
                description = 'A JPEG-2000 image encoder/decoder for Android'
                url = 'https://github.com/gemalto/JP2ForAndroid'
                licenses {
                    license {
                        name = 'The 2-Clause BSD License'
                        url = 'https://opensource.org/licenses/BSD-2-Clause'
                    }
                }
                developers {
                    developer {
                        id = 'michal-dvorak-thales'
                        name = 'Michal Dvorak'
                        email = 'michal.dvorak@thalesgroup.com'
                    }
                }
                scm {
                    connection = 'https://github.com/gemalto/JP2ForAndroid.git'
                    developerConnection = 'https://github.com/gemalto/JP2ForAndroid.git'
                    url = 'https://github.com/gemalto/JP2ForAndroid'
                }
            }
        }
    }
    repositories {
        maven {
            url 'build/repo'
        }
    }
}
