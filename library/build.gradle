plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace = "com.gemalto.jp2"

    compileSdk 34
    defaultConfig {
        minSdkVersion 23
        targetSdk 34

        versionName "1.0.3"
        versionCode 4

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    buildFeatures {
        buildConfig = true
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }
    ndkVersion '23.0.7344513'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.annotation:annotation:1.8.2'
    //testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

/*************************************************************/
/******************* PUBLISHING STUFF ************************/
/*************************************************************/
//create a sources jar
task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

//publish to a local Maven repository with the sources jar and all dependencies
publishing {
    publications {
        aarRelease(MavenPublication) {
            groupId 'com.gemalto.jp2'
            artifactId 'jp2-android'
            version "${android.defaultConfig.versionName}"
            artifact(sourceJar)
            artifact("build/outputs/aar/library-release.aar")
        }
    }
    repositories {
        maven {
            url 'build/repo'
        }
    }
}

ext {
    publishedGroupId = 'com.gemalto.jp2'
    libraryName = 'JP2ForAndroid'
    artifact = 'jp2-android'

    libraryDescription = 'A JPEG-2000 image encoder/decoder for Android'

    siteUrl = 'https://github.com/gemalto/JP2ForAndroid'
    gitUrl = 'https://github.com/gemalto/JP2ForAndroid.git'

    libraryVersion = "${android.defaultConfig.versionName}"

    developerId = 'michal-dvorak-thales'
    developerName = 'Michal Dvorak'
    developerEmail = 'michal.dvorak@thalesgroup.com'

    licenseName = 'The 2-Clause BSD License'
    licenseUrl = 'https://opensource.org/licenses/BSD-2-Clause'
    allLicenses = ["BSD-2-Clause"]
}
